buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath libraries.tomcat_plugin
    }
}

apply plugin: 'com.bmuschko.tomcat-base'

dependencies {
    // tomcat plugin dependencies
    tomcat libraries.tomcat
    providedRuntime libraries.servlet_api

    compile project(':pep-authc-api')
}

ext {
    tomcatStopPort = 8081
    tomcatStopKey = 'stopKey'
}

task integrationTomcatRun(type: com.bmuschko.gradle.tomcat.tasks.TomcatRun) {
    stopPort = tomcatStopPort
    stopKey = tomcatStopKey
    daemon = true
}

task integrationTomcatStop(type: com.bmuschko.gradle.tomcat.tasks.TomcatStop) {
    stopPort = tomcatStopPort
    stopKey = tomcatStopKey
}

task integrationTest(type: Test) {
    include '**/*IntegrationTest.*'
    dependsOn integrationTomcatRun
    finalizedBy integrationTomcatStop
}

build.dependsOn integrationTest

test {
    exclude '**/*IntegrationTest.*'
}