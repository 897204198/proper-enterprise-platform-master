apply plugin: 'checkstyle'
apply plugin: 'codenarc'
apply plugin: 'findbugs'
apply plugin: 'jacoco'
apply plugin: 'pmd'

def configDir = new File(pepDevConfRoot, 'configs/quality')

ext.checkstyleConfigDir = "$configDir/checkstyle"

checkstyle {
    configFile = new File(checkstyleConfigDir, "checkstyle.xml")
    configProperties.checkstyleConfigDir = checkstyleConfigDir
    toolVersion = '8.7'
}

codenarc {
    configFile = new File(configDir, "codenarc.xml")
    toolVersion = '0.25.2'
}

findbugs {
    toolVersion = '3.0.1'
    excludeFilter = file("$configDir/findbugs/excludeFilter.xml")
}

jacoco {
    toolVersion = "0.7.7.201606060606"
}

pmd {
    toolVersion = '5.5.7'
}

jacocoTestReport {
    reports {
        xml.enabled true
    }

    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                '**/RedisCacheManager**',
                '**/AbstractEhcacheRegionFactory**',
                '**/EhCacheRegionFactory**'
            ])
        })
    }
}

configurations.codenarc.resolutionStrategy.force(libraries.groovy_all)

def excludeFlowable = 'org/flowable'
def excludeGzipFromJetty = 'com/proper/enterprise/platform/core/utils/gzip'

tasks.withType(Checkstyle) {
    exclude "**/$excludeFlowable/**"
    exclude "**/$excludeGzipFromJetty/**"
}

tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

tasks.withType(Pmd) {
    exclude "**/$excludeFlowable/**"
    exclude "**/$excludeGzipFromJetty/**"

    ruleSetFiles = files(
        "$configDir/pmd/ali-comment.xml",
        "$configDir/pmd/ali-concurrent.xml",
        "$configDir/pmd/ali-constant.xml",
        "$configDir/pmd/ali-exception.xml",
        "$configDir/pmd/ali-flowcontrol.xml",
        "$configDir/pmd/ali-naming.xml",
        "$configDir/pmd/ali-oop.xml",
        "$configDir/pmd/ali-orm.xml",
        "$configDir/pmd/ali-other.xml",
        "$configDir/pmd/ali-set.xml"
    )

    reports {
        xml.enabled = false
        html.enabled = true
    }
}

check.dependsOn jacocoTestReport
build.dependsOn jacocoTestReport
