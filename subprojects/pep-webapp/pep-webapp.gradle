buildscript {
    repositories {
        maven {
            url "${nexusUrl}/repository/maven-public"
            credentials(PasswordCredentials) {
                username = nexusUsername
                password = nexusPassword
            }
        }
    }
    dependencies {
        classpath 'org.akhikhl.gretty:gretty:1.2.4'
    }
}

apply plugin: 'org.akhikhl.gretty'
apply plugin: 'eclipse-wtp'
apply plugin: 'war'

gretty {
    contextPath = 'pep'
    debugSuspend = false
    fileLogEnabled = false
    jvmArgs = ['-XX:PermSize=256M', '-Xms256m', '-Xmx1024m']
    managedClassReload = true
    serverConfigFile = "$pepDevConfRoot/configs/docker/tomcat/conf/server.xml"
    servletContainer = 'tomcat7'
}

dependencies {
    compile libraries.groovy

    compile project(':pep-auth-api')

    runtime libraries.commons_fileupload,
            libraries.jackson_databind,
            libraries.spring_orm,
            libraries.spring_webmvc

    // Choose by datasource
    runtime libraries.h2,
            libraries.mysql_driver

    // Necessary when using Web Service
    // runtime libraries.jaxws_rt

    // Mandatory for PEP
    runtime project(':pep-configs')

    // Select on demand
    runtime project(':pep-auth-jwt'),
            project(':pep-auth-common-jpa'),
            // Needs one cache implementation if use cache API
            project(':pep-cache-ehcache'),
            project(':pep-page-custom'),
            project(':pep-push'),
            project(':pep-workflow')
}
task cleanDist(type: Delete) {
    delete 'build/libs'
}

compileJava.dependsOn cleanDist
uploadArchives.deleteAllActions()
